apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    signingConfigs {
        gearSigningConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 24
    buildToolsVersion "23.0.3"
    defaultConfig {
        applicationId "com.wubydax.romcontrol.v2"
        minSdkVersion 21
        //noinspection OldTargetApi
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        project.archivesBaseName = "RomControl"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.gearSigningConfig
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

task installAndLaunchRelease(type: Exec, dependsOn: 'installRelease') {
    commandLine 'adb', 'shell', 'am', 'start', '-n', 'com.wubydax.romcontrol/.MainActivity'
}

task pushToDevice(type: Exec, dependsOn: 'assembleRelease') {
    commandLine 'adb', 'push', './build/outputs/apk/RomControl-release.apk', '/sdcard/RomControl-release.apk'
}

task mountRW(type: Exec, dependsOn: 'pushToDevice') {
    commandLine 'adb', 'shell', 'su', '-c', 'mount -o remount,rw /system'
}

task copyToPrivApp(type: Exec, dependsOn: 'mountRW') {
    commandLine 'adb', 'shell', 'su', '-c', 'cp /storage/emulated/0/RomControl-release.apk /system/priv-app/RomControl.apk'
    finalizedBy('chmodApk')
}

task chmodApk(type: Exec) {
    commandLine 'adb', 'shell', 'su', '-c', 'chmod 644 /system/priv-app/RomControl.apk'
    finalizedBy('mountRO')
}

task mountRO(type: Exec) {
    commandLine 'adb', 'shell', 'su', '-c', 'mount -o remount,ro /system'
    finalizedBy('rebootDevice')
}

task rebootDevice(type: Exec) {
    commandLine 'adb', 'reboot'
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:support-v4:24.0.0'
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:cardview-v7:24.0.0'
    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile files('libs/RootTools.jar')
}
